plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
}

group = 'ai'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExtensions
}

repositories {
    mavenCentral()
}

ext {
//    set('snippetsDir', file("build/generated-snippets"))
    snippetsDir = file("build/generated-snippets")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
//    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    implementation 'org.testcontainers:testcontainers:1.18.3'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
    testImplementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    testImplementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    testImplementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    // querydsl
    implementation 'com.querydsl:querydsl-core:5.0.0'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // sonarqube
    implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3'

    implementation platform('software.amazon.awssdk:bom:2.17.230')
    implementation 'software.amazon.awssdk:s3'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'io.projectreactor:reactor-test'

    testImplementation 'com.squareup.okhttp3:okhttp:4.11.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'

    // https://mvnrepository.com/artifact/io.netty/netty-resolver-dns-native-macos
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.68.Final:osx-aarch_64'

    // log
    implementation 'p6spy:p6spy:3.9.1'

    // mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    implementation 'org.springframework.data:spring-data-envers'
}

def querydslDir = "src/main/generated"

clean {
    delete file(querydslDir)
}

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor {
    inProcess = JAVA_EXEC
    forkOptions {
        jvmArgs("--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED")
    }
    configurations 'asciidoctorExtensions'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from "${asciidoctor.outputDir}"
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports {
        xml.required.set(true)  // sonarqube : true
        csv.required.set(false)
        html.required.set(true)

        xml.destination file("${buildDir}/jacoco/index.xml")
        csv.destination file("${buildDir}/jacoco/index.csv")
        html.destination file("${buildDir}/jacoco/index.html")
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/*Application*',
                            '**/*Exception*',
                            '**/*Advice*',
                            '**/*dto*',
                            '**/*request/**',
                            '**/*response/**',
                            '**/enums/**',
                            '**/api/**',
                            '**/global/**',
                            '**/domain/**',
                    ])
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            excludes = [
                    '*.*Application',
                    '*.*Exception',
                    '*.*Advice',
                    '*.request.*',
                    '*.response.*',
                    '*.vo.*',
                    '*.port.*',
                    '*.repository.*',
                    '*.enums.*',
                    '*.api.*',
                    '*.global.*',
                    '*.security.*',
                    '*.domain.*',
                    '*.domain.*.Q*',
                    '*.*CustomRepository',
            ]

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }

        rule {
            enabled = true
            element = 'CLASS'
            excludes = [
                    '*.*Application',
                    '*.*Exception',
                    '*.*Advice',
                    '*.request.*',
                    '*.response.*',
                    '*.vo.*',
                    '*.port.*',
                    '*.repository.*',
                    '*.enums.*',
                    '*.api.*',
                    '*.global.*',
                    '*.security.*',
                    '*.domain.*',
                    '*.domain.*.Q*',
                    '*.*CustomRepository',
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }
    }
}


sonarqube {
    properties {
        property "sonar.host.url", "http://3.36.90.51:9000/"
        property "sonar.sources", "src"
        property "sonar.language", "java"
        property "sonar.projectKey", "server_api"
        property "sonar.projectName", "server_api"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.binaries", "${buildDir}/classes"
        property "sonar.test.inclusions", "**/*Test.java, **/*Docs.java"
        property "sonar.exclusions", "**/resources/static/**, **/Q*.class, **/test/**, **/security/**, **/webconfig/**, **/generated/**"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/jacoco/index.xml"
    }
}